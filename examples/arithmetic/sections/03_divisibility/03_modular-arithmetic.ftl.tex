\documentclass[../../natural-numbers.ftl.tex]{subfiles}

\begin{document}

  \section{Modular arithmetic}

  \begin{forthel}
    [readtex \path{arithmetic/sections/03_divisibility/02_euclidean-division.ftl.tex}]
  \end{forthel}

  \begin{forthel}
    Let $k, k', l, m, n, n', n''$ denote natural numbers.
  \end{forthel}


  \begin{forthel}
    \begin{definition}
      Let $k$ be nonzero.
      $n \equiv m \pmod{k}$ iff $n \mod k = m \mod k$.
    \end{definition}

    Let $n$ and $m$ are congruent modulo $k$ stand for $n \equiv m \pmod{k}$.

    \begin{proposition}[NN 03 03 188421]
      Let $m$ be nonzero.
      Then \[ n \equiv n \pmod{m}. \]
    \end{proposition}
    \begin{proof}
      We have $n \mod m = n \mod m$.
      $n \equiv n \pmod{m}$.
    \end{proof}

    \begin{proposition}[NN 03 03 880545]
      Let $m$ be nonzero.
      Then \[ n \equiv n' \pmod{m} \implies n' \equiv n \pmod{m}. \]
    \end{proposition}
    \begin{proof}
      Assume $n \equiv n' \pmod{m}$.
      Then $n \mod m = n' \mod m$.
      Hence $n' \mod m = n \mod m$.
      Thus $n' \equiv n \pmod{m}$.
    \end{proof}

    \begin{proposition}[NN 03 03 310316]
      Let $m$ be nonzero.
      Then \[ (\text{$n \equiv n' \pmod{m}$ and $n' \equiv n'' \pmod{m}$}) \implies n \equiv n'' \pmod{m}. \]
    \end{proposition}
    \begin{proof}
      Assume $n \equiv n' \pmod{m}$ and $n' \equiv n'' \pmod{m}$.
      Then $n \mod m = n' \mod m$ and $n' \mod m = n'' \mod m$.
      Hence $n \mod m = n'' \mod m$.
      Thus $n \equiv n'' \pmod{m}$.
    \end{proof}

    \begin{proposition}[NN 03 03 376294]
      Let $k$ be nonzero.
      Assume $n \geq m$.
      Then $n \equiv m \pmod{k}$ iff $n = (k \cdot x) + m$ for some natural number $x$.
    \end{proposition}
    \begin{proof}
      Case $n \equiv m \pmod{k}$.
        Then $n \mod k = m \mod k$.
        Take a natural number $r$ such that $r < k$ and $n \mod k = r = m \mod k$.
        Take a nonzero natural number $l$ such that $k = r + l$.
        Consider natural numbers $q,q'$ such that $n = (q \cdot k) + r$ and $m = (q' \cdot k) + r$.

        Then $q \geq q'$. \\
        Proof.
          Assume the contrary.
          Then $q < q'$.
          Hence $q \cdot k < q' \cdot k$.
          Thus $(q \cdot k) + r < (q' \cdot k) + r$.
          Therefore $n < m$.
          Contradiction.
        Qed.

        Take a natural number $x$ such that $q = q' + x$.

        Let us show that $n = (k \cdot x) + m$.
          We have
          \[   (k \cdot x) + m \]
          \[ = (k \cdot x) + ((q' \cdot k) + r) \]
          \[ = ((k \cdot x) + (q' \cdot k)) + r \]
          \[ = ((k \cdot x) + (k \cdot q')) + r \]
          \[ = (k \cdot (q' + x)) + r \]
          \[ = (k \cdot q) + r \]
          \[ = n. \]
        End.
      End.

      Case $n = (k \cdot x) + m$ for some natural number $x$.
        Consider a natural number $x$ such that $n = (k \cdot x) + m$.
        Take natural numbers $r,r'$ such that $n \mod k = r$ and $m \mod k = r'$.
        Then $r,r' < k$.
        Take natural numbers $q,q'$ such that $n = (k \cdot q) + r$ and $m = (k \cdot q') + r'$.
        Then
        \[   (k \cdot q) + r \]
        \[ = n \]
        \[ = (k \cdot x) + m \]
        \[ = (k \cdot x) + ((k \cdot q') + r') \]
        \[ = ((k \cdot x) + (k \cdot q')) + r' \]
        \[ = (k \cdot (x + q')) + r'. \]
        Hence $r = r'$.
        Thus $n \mod k = m \mod k$.
        Therefore $n \equiv m \pmod{k}$.
      End.
    \end{proof}

    \begin{proposition}[NN 03 03 665599]
      Let $k,k'$ be nonzero.
      Then \[ n \equiv m \pmod{k \cdot k'} \implies n \equiv m \pmod{k}. \]
    \end{proposition}
    \begin{proof}
      Assume $n \equiv m \pmod{k \cdot k'}$.

      Case $n \geq m$.
        We can take a natural number $x$ such that $n = ((k \cdot k') \cdot x) + m$.
        Then $n = (k \cdot (k' \cdot x)) + m$.
        Hence $n \equiv m \pmod{k}$.
      End.

      Case $m \geq n$.
        We have $m \equiv n \pmod{k \cdot k'}$.
        Hence we can take a natural number $x$ such that $m = ((k \cdot k') \cdot x) + n$.
        Then $m = (k \cdot (k' \cdot x)) + n$.
        Thus $m \equiv n \pmod{k}$.
        Therefore $n \equiv m \pmod{k}$.
      End.
    \end{proof}

    \begin{corollary}[NN 03 03 282418]
      Let $k,k'$ be nonzero natural numbers.
      Then \[ n \equiv m \pmod{k \cdot k'} \implies n \equiv m \pmod{k'}. \]
    \end{corollary}
    \begin{proof}
      Assume $n \equiv m \pmod{k \cdot k'}$.
      Then $n \equiv m \pmod{k' \cdot k}$.
      Hence $n \equiv m \pmod{k'}$.
    \end{proof}
  \end{forthel}
\end{document}
